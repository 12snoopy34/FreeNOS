#
# Copyright (C) 2015 Niek Linnenbank
# 
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#

Import('build_env')
Import('rootfs_files')

env = build_env.Clone()

for item in env['LINKUSER']:
    env['LINKFLAGS'].remove(item)

env.Append(LINKFLAGS = env['LINKKERN'])
env.Append(CPPFLAGS = '-D__KERNEL__')

env.UseLibraries(['liballoc', 'libstd'])
env.UseServers(['process', 'filesystem', 'memory'])
env.TargetProgram('kernel', [ Glob('*.cpp'),
			      Glob('*.S'),
			      Glob('#' + env['BUILDROOT'] + '/kernel/*.cpp'),
			      Glob('#' + env['BUILDROOT'] + '/kernel/API/*.cpp') ]
		          , env['boot'])

#
# Boot Image
#
env.BootImage('#${BUILDROOT}/boot.img.gz', '#kernel/x86/pc/boot.imgdesc')

#
# RootFS
#
env.LinnImage('#${BUILDROOT}/boot.linn.gz', rootfs_files)
env.Depends('#${BUILDROOT}/boot.linn.gz', '#build/host')

#
# Boot ISO (LiveCD)
#
cd = env.ISO('#${BUILDROOT}/boot.iso',
           [ '#kernel/x86/pc/stage1',
             '#kernel/x86/pc/stage2_eltorito',
             '#kernel/x86/pc/menu.lst',
             '#${BUILDROOT}/kernel/x86/pc/kernel',
             '#${BUILDROOT}/boot.img.gz',
	     '#${BUILDROOT}/boot.linn.gz' ])

# Shortcut to build the ISO.
Alias('iso', cd)

#
# Qemu targets
#
env.Targets(qemu         = 'qemu-system-i386 -cdrom ' + build_env['BUILDROOT'] + '/boot.iso')
env.Targets(qemu_curses  = 'qemu-system-i386 -cdrom ' + build_env['BUILDROOT'] + '/boot.iso -curses')
